{*
 *  URUWorks Subtitle API
 *
 *  The contents of this file are used with permission, subject to
 *  the Mozilla Public License Version 2.0 (the "License"); you may
 *  not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *  http://www.mozilla.org/MPL/2.0.html
 *
 *  Software distributed under the License is distributed on an
 *  "AS IS" basis, WITHOUT WARRANTY OF ANY KIND, either express or
 *  implied. See the License for the specific language governing
 *  rights and limitations under the License.
 *
 *  Copyright (C) 2001-2023 URUWorks, uruworks@gmail.com.
 *}

unit UWSubtitleAPI.Formats.AdvancedSubstationAlpha;

// -----------------------------------------------------------------------------

interface

uses
  SysUtils, UWSubtitleAPI, UWSystem.TimeUtils, UWSubtitleAPI.Formats;

type

  { TUWAdvancedSubstationAlpha }

  TUWAdvancedSubstationAlpha = class(TUWSubtitleCustomFormat)
  public
    function Name: String; override;
    function Format: TUWSubtitleFormats; override;
    function Extension: String; override;
    function IsTimeBased: Boolean; override;
    function HasStyleSupport: Boolean; override;
    function IsMine(const SubtitleFile: TUWStringList; const Row: Integer): Boolean; override;
    function LoadSubtitle(const SubtitleFile: TUWStringList; const FPS: Double; var Subtitles: TUWSubtitles): Boolean; override;
    function SaveSubtitle(const FileName: String; const FPS: Double; const Encoding: TEncoding; const Subtitles: TUWSubtitles; const SubtitleMode: TSubtitleMode; const FromItem: Integer = -1; const ToItem: Integer = -1): Boolean; override;
  end;

// -----------------------------------------------------------------------------

implementation

uses strUtils, UWSystem.StrUtils, UWSystem.SysUtils;

const
  SSA_EventFormat = 'Format: Layer, Start, End, Style, Actor, MarginL, MarginR, MarginV, Effect, Text';
  SSA_StyleFormat = 'Format: Name, Fontname, Fontsize, PrimaryColour, SecondaryColour, OutlineColour, BackColour, Bold, Italic, Underline, StrikeOut, ScaleX, ScaleY, Spacing, Angle, BorderStyle, Outline, Shadow, Alignment, MarginL, MarginR, MarginV, Encoding';

// -----------------------------------------------------------------------------

function TUWAdvancedSubstationAlpha.Name: String;
begin
  Result := IndexToName(Integer(Format));
end;

// -----------------------------------------------------------------------------

function TUWAdvancedSubstationAlpha.Format: TUWSubtitleFormats;
begin
  Result := TUWSubtitleFormats.sfAdvancedSubstationAlpha;
end;

// -----------------------------------------------------------------------------

function TUWAdvancedSubstationAlpha.Extension: String;
begin
  Result := '*.ass';
end;

// -----------------------------------------------------------------------------

function TUWAdvancedSubstationAlpha.IsTimeBased: Boolean;
begin
  Result := True;
end;

// -----------------------------------------------------------------------------

function TUWAdvancedSubstationAlpha.HasStyleSupport: Boolean;
begin
  Result := True; // Has tags
end;

// -----------------------------------------------------------------------------

function TUWAdvancedSubstationAlpha.IsMine(const SubtitleFile: TUWStringList; const Row: Integer): Boolean;
begin
  if (Pos('Dialogue:', SubtitleFile[Row]) > 0) and
     (Pos('Marked=', SubtitleFile[Row]) = 0) and
     (Pos('!effect', SubtitleFile[Row]) = 0) and
     (TimeInFormat(Trim(Copy(SubtitleFile[Row], Pos(',', SubtitleFile[Row]) + 1, PosEx(',', SubtitleFile[Row], Pos(',', SubtitleFile[Row]) + 1) - (Pos(',', SubtitleFile[Row]) + 1))), 'h:mm:ss.zz'))  and
     (TimeInFormat(Trim(Copy(SubtitleFile[Row], PosEx(',', SubtitleFile[Row], Pos(',', SubtitleFile[Row]) + 1) + 1, PosEx(',', SubtitleFile[Row], PosEx(',', SubtitleFile[Row], Pos(',', SubtitleFile[Row]) + 1) + 1) - (PosEx(',', SubtitleFile[Row], Pos(',', SubtitleFile[Row]) + 1) + 1))), 'h:mm:ss.zz')) then
    Result := True
  else
    Result := False;
end;

// -----------------------------------------------------------------------------

function TUWAdvancedSubstationAlpha.LoadSubtitle(const SubtitleFile: TUWStringList; const FPS: Double; var Subtitles: TUWSubtitles): Boolean;
var
  i, a        : Integer;
  InitialTime : Integer;
  FinalTime   : Integer;
  Text, Actor : String;
begin
  Result := False;
  try
    i := 0;
    while i < SubtitleFile.Count do
    begin
      if Contains('Dialogue:', SubtitleFile[i]) then
      begin
        a := Pos(',', SubtitleFile[i]);
        InitialTime := StringToTime(Trim(Copy(SubtitleFile[i], a + 1, PosEx(',', SubtitleFile[i], a + 1) - (a + 1))));
        a := PosEx(',', SubtitleFile[i], a + 1);
        FinalTime   := StringToTime(Trim(Copy(SubtitleFile[i], a + 1, PosEx(',', SubtitleFile[i], a + 1) - (a + 1))));

        if (InitialTime >= 0) and (FinalTime > 0) then
        begin
          Text := SubtitleFile[i];
          for a := 1 to 4 do Delete(Text, 1, Pos(',', Text));
          Actor := Copy(Text, 1, Pos(',', Text)-1);
          for a := 1 to 5 do Delete(Text, 1, Pos(',', Text));

          Text := ReplaceString(Trim(Text), '\N', LineEnding);

          a := Subtitles.Add(InitialTime, FinalTime, Text, '', NIL);
          Subtitles.ItemPointer[a]^.Actor := Actor;
        end;
      end;
      Inc(i);
    end;
  finally
    if Subtitles.Count > 0 then Result := True;
  end;
end;

// -----------------------------------------------------------------------------

function TUWAdvancedSubstationAlpha.SaveSubtitle(const FileName: String; const FPS: Double; const Encoding: TEncoding; const Subtitles: TUWSubtitles; const SubtitleMode: TSubtitleMode; const FromItem: Integer = -1; const ToItem: Integer = -1): Boolean;
var
  i : Integer;
  Text, s : String;
begin
  Result  := False;

  StringList.Add('[Script Info]');
  StringList.Add('ScriptType: v4.00+');
//  StringList.Add('Collisions: ' + ASSAttributes.Collisions);
//  StringList.Add('PlayResX: ' + IntToStr(ASSAttributes.PlayResX));
//  StringList.Add('PlayResY: ' + IntToStr(ASSAttributes.PlayResY));
//  StringList.Add('Timer: ' + ASSAttributes.Timer);
  StringList.Add('');
  StringList.Add('[V4+ Styles]');
  StringList.Add(SSA_StyleFormat);
  StringList.Add('Style: ' + Subtitles.FormatProperties^.SSA.DefaultStyleSettings);
  StringList.Add('');
  StringList.Add('[Events]');
  StringList.Add(SSA_EventFormat);

  for i := FromItem to ToItem do
  begin
    Text := iff(SubtitleMode = smText, Subtitles.Text[i], Subtitles.Translation[i]);
    Text := ReplaceString(Trim(Text), LineEnding, '\N');

    s := '';
    if Subtitles[i].Align <> shaNone then
    begin
      case Subtitles[i].Align of
        shaLeft : case Subtitles[i].VAlign of
                    svaCenter : s := '{\an4}';
                    svaTop    : s := '{\an7}';
                  else
                    s := '{\an1}';
           end;
        shaCenter : case Subtitles[i].VAlign of
                      svaCenter : s := '{\an5}';
                      svaTop    : s := '{\an8}';
                    else
                      s := '{\an2}';
                    end;
        shaRight : case Subtitles[i].VAlign of
                     svaCenter : s := '{\an6}';
                     svaTop    : s := '{\an9}';
                   else
                     s := '{\an3}';
                   end;
      end;
    end
    else if Subtitles[i].VAlign <> svaBottom then
    begin
      case Subtitles[i].VAlign of
        svaCenter : s := '{\an5}';
        svaTop    : s := '{\an8}';
      else
        s := '{\an2}';
      end;
    end;

    if not s.IsEmpty then
      Text := s + Text + '{\an0}';

    StringList.Add('Dialogue: 0,' + TimeToString(iff(Subtitles.Tag = 0, Subtitles.InitialTime[i], Subtitles.InitialTime[i] - 1), 'h:mm:ss.zz') + ',' + TimeToString(iff(Subtitles.Tag = 0, Subtitles.FinalTime[i], Subtitles.FinalTime[i] - 1), 'h:mm:ss.zz') + ',Default,' + Subtitles[i].Actor + ',0000,0000,0000,,' + Text);
  end;

  try
    StringList.SaveToFile(FileName, Encoding);
    Result := True;
  except
  end;
end;

// -----------------------------------------------------------------------------

end.
